apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
  compileSdk 30

  defaultConfig {
    applicationId "com.timejar.app"
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    // Load secrets.properties file
    def secretsPropertiesFile = rootProject.file("secrets.properties")
    if (secretsPropertiesFile.exists()) {
      def secretsProperties = new Properties()
      secretsProperties.load(new FileInputStream(secretsPropertiesFile))

      // Access properties and add them as build config fields
      secretsProperties.each { key, value ->
        buildConfigField "String", key.toString(), "\"${value}\""
      }
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  buildFeatures {
    viewBinding true
    buildConfig true
  }
    namespace 'com.timejar.app'
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])

  // Kotlin
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  // Support Libraries
  implementation 'androidx.appcompat:appcompat:1.3.0'
  implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

  // Testing Dependencies
  testImplementation 'junit:junit:4.13.1'
  androidTestImplementation 'androidx.test.ext:junit:1.1.2'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

  // TUTORIAL DEPENDENCIES HERE
  implementation 'com.google.android.gms:play-services-location:18.0.0'

  // Supabase
  implementation platform("io.github.jan-tennert.supabase:bom:1.4.7")
  implementation 'io.github.jan-tennert.supabase:gotrue-kt'
  implementation 'io.github.jan-tennert.supabase:realtime-kt'
  implementation 'io.github.jan-tennert.supabase:functions-kt'
  implementation 'io.github.jan-tennert.supabase:postgrest-kt'
  implementation 'io.github.jan-tennert.supabase:compose-auth'
  implementation 'io.github.jan-tennert.supabase:compose-auth-ui'

  implementation 'io.ktor:ktor-client-android:2.3.6'

}
